buildscript {
    ext {
        springBootVersion = "3.5.5"
        httpclientVersion = "4.5.14"
        aerospikeClientVersion = findProperty("aerospikeClientVersion") ?: "9.0.5"
    }
    if (findProperty("aerospikeUseLocal")) {
        print("using Local repo")
        repositories {
            mavenLocal()
        }
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}


plugins {
    id("application")
    id('com.palantir.git-version') version "3.1.0"
    id("org.springdoc.openapi-gradle-plugin") version "1.9.0"
    id("com.github.spotbugs") version "6.4.8"
    id("org.owasp.dependencycheck") version "11.0.0"
}

if (findProperty("aerospikeUseLocal")) {
    print("using Local repo")
    repositories {
        mavenLocal()
    }
}
repositories {
    mavenCentral()
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "jacoco"

group = "com.aerospike"
sourceCompatibility = 17

def gitVersionDetails = versionDetails()
def gitVersion = gitVersionDetails.lastTag
if (gitVersionDetails.commitDistance != 0) {
    gitVersion = gitVersion + "-" + gitVersionDetails.commitDistance + "-" + gitVersionDetails.gitHash
}
version = gitVersion

bootJar {
    archiveBaseName = "${jarBaseName}"
}
jar {
    archiveBaseName = "${jarBaseName}"
}
test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                counter = "LINE"
                value = "COVEREDRATIO"
                minimum = BigDecimal.valueOf(0.50)
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

springBoot {
    buildInfo()
}

openApi {
    customBootRun {
        args.set(["--aerospike.restclient.requireAuthentication=true"])
    }
}

dependencies {
    implementation('org.msgpack:msgpack-core:0.9.10')
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    implementation("com.aerospike:aerospike-client-jdk8:${aerospikeClientVersion}")
    implementation("com.aerospike:aerospike-document-api:2.0.3")
    implementation("org.msgpack:jackson-dataformat-msgpack:0.9.10")
    implementation('org.springframework.retry:spring-retry:2.0.12')
    implementation('org.springframework:spring-aspects:6.2.10')
    implementation('org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j:3.3.0')
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13')
    implementation("javax.inject:javax.inject:1")
    implementation("com.google.guava:guava:33.4.8-jre")
    implementation("org.springframework.boot:spring-boot-starter-validation:${springBootVersion}")
    implementation("org.apache.httpcomponents:httpclient:${httpclientVersion}")
    implementation("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")
    implementation('io.github.resilience4j:resilience4j-micrometer:2.2.0')
    implementation('io.micrometer:micrometer-registry-prometheus')
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine")
}

compileJava {
    options.compilerArgs << '-parameters'
}

spotbugsMain {
    reports {
        html.required = true
    }
}
spotbugsTest {
    reports {
        html.required = true
dependencyCheck {
    failBuildOnCVSS = 7.0
    suppressionFile = file("${rootDir}/config/dependency-check-suppressions.xml")
    // NVD API key (optional): request at https://nvd.nist.gov/developers/request-an-api-key
    // Set NVD_API_KEY env var in CI (e.g. GitHub Actions secret) to speed up CVE updates
    nvd {
        apiKey = System.getenv("NVD_API_KEY") ?: null
    }
}
